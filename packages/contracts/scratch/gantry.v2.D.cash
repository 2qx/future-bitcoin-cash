pragma cashscript ^0.10.0;

//
// DETERMISTIC Gantry - Create vault contracts with fungible tokens in a uniform way. 
//
// From: Future Bitcoin Cash
//
// Author: 2qx <2qx_in_the_future@small.neomailbox.ch>
//
// NFT commentment stores the next series locktime in 32-bit LE
//
// [ ] Require the minting baton in the input
// [ ] Get the current step increment for the chain of futures
// [ ] Get the current vault locktime to be printed.
//
//   either
// [ ] Mint an array of FT utxos, 
// [ ] send them off to a Vault
//
//   or
// [ ] skip every 10th print.
//
// [ ] increment locktime height value stored on NFT baton
// [ ] assure NFT baton is returned
//
//
//  Gantry i/o Flow:
//
//  Inputs              Outputs
//  [0] NFT mintBaton   ->  [0] NFT mintBaton
//  [1] topup sats?     =>  [1] FTs Vault
//                          [2] FTs Vault
//                          [3] FTs Vault
//                          
//  
//  ... but skip every 10th token print, 
//   which will be printed by the gantry of the next order.
//  [0] NFT mintBaton   =>  [0] NFT mintBaton
//


contract Gantry(
    bytes4 stepBytes, 
    bytes vaultUnlockingBytecode
    ) {

    function execute(){
        
        // All from a single input
        require(tx.inputs.length == 1);
        
        // Gantry covenant must be passed on
        // in first outputs[0] in any case.
        require(tx.inputs[0].lockingBytecode == tx.outputs[0].lockingBytecode);

        // Return mutable NFT baton
        // to outputs[0] in any case.
        require(tx.inputs[0].tokenCategory == tx.outputs[0].tokenCategory);

        // Allow 5k sats for all expenditures
        require(tx.outputs[0].value == (tx.inputs[0].value - 5000));     

        // simply return the NFT baton without minting FTs
        require(tx.outputs[0].tokenAmount == 0);

        int step = int(stepBytes);
        int locktime = int(tx.inputs[0].nftCommitment);

        // Height stored in mutable NFT commitment MUST be incremented by <step>.
        require(tx.outputs[0].nftCommitment == bytes4(locktime+step));

        // if not a 10th step, mint tokens
        if((locktime/step)%10==0){ 
            require(tx.outputs.length == 1);
        } else {
            // Get the redeem bytecode of the vault instance
            bytes theVault = 
                0x04 + bytes4(tx.inputs[0].nftCommitment) +     // locktime
                vaultUnlockingBytecode;    

            bytes vaultLockingBytecode = 0xaa20 + hash256(theVault) + 0x87;

            // Shard out tokens across three UTXOs
            require(tx.outputs[1].lockingBytecode == vaultLockingBytecode);       
            require(tx.outputs[1].tokenCategory == tx.inputs[0].outpointTransactionHash);
            require(tx.outputs[1].tokenAmount == 700000000000000);     
            require(tx.outputs[1].nftCommitment == 0x);    
            require(tx.outputs[1].value == 1000);     

            require(tx.outputs[2].lockingBytecode == vaultLockingBytecode);       
            require(tx.outputs[2].tokenCategory == tx.inputs[0].outpointTransactionHash);
            require(tx.outputs[2].tokenAmount == 700000000000000);  
            require(tx.outputs[2].nftCommitment == 0x); 
            require(tx.outputs[2].value == 1000);  

            require(tx.outputs[3].lockingBytecode == vaultLockingBytecode);       
            require(tx.outputs[3].tokenCategory == tx.inputs[0].outpointTransactionHash);
            require(tx.outputs[3].tokenAmount == 700000000000000); 
            require(tx.outputs[3].nftCommitment == 0x); 
            require(tx.outputs[3].value == 1000);   

            require(tx.outputs.length == 4);  

        }     
        
    }
}